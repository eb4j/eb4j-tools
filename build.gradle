// eb4j gradle build script.
//
// Copyright (c) 2016,2020-2021 Hiroshi Miura
//

plugins {
    id 'java'
    id 'jacoco'
    id 'maven'
    id 'distribution'
    id 'checkstyle'
    id 'application'
}

group = projectGroup
version = projectVersion

repositories {
    mavenCentral()
}

task ebzipStartScript(type: CreateStartScripts) {
    mainClassName = 'io.github.eb4j.tool.EBZip'
    applicationName = 'ebzip'
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
}

task ebdumpStartScript(type: CreateStartScripts) {
    mainClassName = 'io.github.eb4j.tool.EBDump'
    applicationName = 'ebdump'
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
}

task ebinfoStartScript(type: CreateStartScripts) {
    mainClassName = 'io.github.eb4j.tool.EBInfo'
    applicationName = 'ebinfo'
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
}

applicationDistribution.into("bin") {
    duplicatesStrategy= DuplicatesStrategy.EXCLUDE
    from(ebzipStartScript)
    from(ebdumpStartScript)
    from(ebinfoStartScript)
    fileMode = 0755
}

dependencies {
    implementation 'io.github.eb4j:eb4j:2.1.10'
    implementation 'info.picocli:picocli:4.6.1'
    annotationProcessor 'info.picocli:picocli-codegen:4.6.1'
    testImplementation 'commons-io:commons-io:2.7'
    testImplementation 'org.testng:testng:7.4.0'
}
test.useTestNG()

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

distTar {
    compression = Compression.BZIP2
}

distributions {
    main.contents {
        from('README.md')
        from('LICENSE.txt')
    }
}

checkstyle {
    config = resources.text.fromFile("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.16.1'
}
