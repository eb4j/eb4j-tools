// eb4j gradle build script.
//
// Copyright (c) 2016,2020-2021 Hiroshi Miura
//

plugins {
    id 'java'
    id 'jacoco'
    id 'maven'
    id 'distribution'
    id 'checkstyle'
    id 'application'
    id 'com.palantir.git-version' version "0.12.3"
    id 'org.mikeneck.graalvm-native-image' version 'v1.4.0'
}

// calculate version string from git tag, hash and commit distance
// It treat as release tag when current HEAD has a tag,
// and repository is clean, no modification,and no untracked files,
if (versionDetails().isCleanTag) {
    // drop first 'v' from version tag
    version = gitVersion().substring(1)
} else {
    version = versionDetails().lastTag.substring(1) + '-' + versionDetails().commitDistance + '-' + versionDetails().gitHash + '-SNAPSHOT'
}

group = projectGroup

repositories {
    mavenCentral()
    mavenLocal()
}

application {
    applicationName = "eb"
    mainClassName("io.github.eb4j.tool.Main")
}

applicationDistribution.into("bin") {
    duplicatesStrategy= DuplicatesStrategy.EXCLUDE
    fileMode = 0755
}

dependencies {
    implementation('io.github.eb4j:eb4j:2.1.10')
            { exclude group: 'org.slf4j', module: 'slf4j-nop' }
    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.12.4'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.4'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.12.4'
    implementation 'info.picocli:picocli:4.6.1'
    implementation 'org.slf4j:slf4j-simple:1.7.32'
    annotationProcessor 'info.picocli:picocli-codegen:4.6.1'
    testImplementation 'commons-io:commons-io:2.7'
    testImplementation 'org.testng:testng:7.4.0'
}
test.useTestNG()

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

nativeImage {
  graalVmHome = System.getProperty('java.home')
  buildType { build ->
    build.executable {
      main = 'io.github.eb4j.tool.Main'
    }
  }
  executableName = 'eb'
  outputDirectory = file("$buildDir/bin")
}

distTar {
    compression = Compression.BZIP2
}

distributions {
    main.contents {
        from('README.md')
        from('LICENSE.txt')
    }
}

checkstyle {
    config = resources.text.fromFile("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.16.1'
}
